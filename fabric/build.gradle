import org.gradle.api.artifacts.VersionCatalogsExtension

plugins {
    alias(libs.plugins.loom)
}

def libsCatalog = project.extensions.getByType(VersionCatalogsExtension).named("libs")

// Properly include common sources and resources in the main source set
sourceSets {
    main {
        java {
            srcDirs += project(":common").sourceSets.main.java.srcDirs
        }
        resources {
            srcDirs += project(":common").sourceSets.main.resources.srcDirs
        }
    }
}

dependencies {
    minecraft(libs.minecraft)
    mappings(loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libsCatalog.findVersion('parchment-minecraft').get().requiredVersion}:${libsCatalog.findVersion('parchment').get().requiredVersion}@zip")
    })
    modImplementation(libs.fabric)
    modImplementation(libs.fabric.api)
    compileOnly(project(":common"))
}

loom {
    runs {
        configureEach {
            runDir("runs/$name")
            ideConfigGenerated(true)
            configName = "Fabric ${name.capitalize()}"
        }

        client {
            client()
            programArg("--username=Dev")
        }

        server {
            server()
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    exclude("**/accesstransformer.cfg")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = base.archivesName.get()
        }
    }
}
