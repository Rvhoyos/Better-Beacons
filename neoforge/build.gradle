import org.gradle.api.artifacts.VersionCatalogsExtension

plugins {
    alias(libs.plugins.moddevgradle)
}

def libsCatalog = project.extensions.getByType(VersionCatalogsExtension).named("libs")

// Properly include common sources and resources in the main source set
sourceSets {
    main {
        java {
            srcDirs += project(":common").sourceSets.main.java.srcDirs
        }
        resources {
            srcDirs += project(":common").sourceSets.main.resources.srcDirs
        }
    }
}

neoForge {
    version = libsCatalog.findVersion('neoforge').get().requiredVersion

    parchment.minecraftVersion = libsCatalog.findVersion('parchment-minecraft').get().requiredVersion
    parchment.mappingsVersion = libsCatalog.findVersion('parchment').get().requiredVersion

    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', 'betterbeacons')
            programArgument('--nogui')
        }

        client {
            client()
            programArguments.addAll('--username', 'Dev')
        }

        server {
            server()
        }
    }

    mods {
        betterbeacons {
            sourceSet project.sourceSets.main
        }
    }
}

dependencies {
    compileOnly(project(":common"))
}

tasks.withType(ProcessResources).configureEach {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = base.archivesName.get()
        }
    }
}
